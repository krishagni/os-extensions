import org.apache.tools.ant.filters.ReplaceTokens
import org.apache.tools.ant.taskdefs.condition.Os
import static groovy.io.FileType.FILES

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'

task fatJar(type: Jar) {
  baseName = 'zos-washu'
  version =  '6.1.0'

  manifest {
    attributes("os-plugin-name": "washu")
  }

  doFirst {
    from ('./src/main/webapp/app/') {
      include '**/**'
 
      into 'META-INF/resources/washu'
    }

    from('./src/main/ui/dist') {
      include '**/**'

      into 'META-INF/resources/ui/washu'
    }
  }

  from {
    configurations.compile.filter {
      it.getAbsolutePath().contains('ca.uhn.hapi') ||
      it.getAbsolutePath().contains('commons-collections4') ||
      it.getAbsolutePath().contains('poi-') ||
      it.getAbsolutePath().contains('xmlbeans')
    }.collect {
      it.isDirectory() ? it : zipTree(it)
    }
  } with jar
}

artifacts {
  archives fatJar
}


sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
}

configurations {
  ajc
  aspects
  aspectCompile
  
  compile {
    extendsFrom aspects
  }
}

configurations.all {
  transitive = false
}

def springVersion = "3.2.13.RELEASE"

dependencies {
  ajc       "org.aspectj:aspectjtools:1.8.6"
  compile   "org.aspectj:aspectjrt:1.8.6"
  compile   "javax.persistence:persistence-api:1.0.2"
  aspects   "org.springframework:spring-aspects:3.2.13.RELEASE"
  aspects   fileTree(dir: '../lib', include: ['openspecimen.jar', 'DynamicExtensions.jar'])
  
  compile group: 'javax.servlet', name: 'javax.servlet-api', version:'3.0.1'

  compile group: 'asm', name: 'asm', version:'3.3.1'
  compile group: 'org.aspectj', name: 'aspectjweaver', version:'1.8.6'
  compile group: 'org.javassist', name: 'javassist', version:'3.19.0-GA'
  compile group: 'cglib', name: 'cglib', version:'3.1'
  compile group: 'com.google.code.gson', name: 'gson', version:'2.3.1'

  compile group: 'org.apache.commons', name: 'commons-collections4', version: '4.1'
  compile group: 'commons-logging', name: 'commons-logging', version:'1.2'
  compile group: 'commons-lang', name: 'commons-lang', version:'2.6'
  compile group: 'org.apache.commons', name: 'commons-lang3', version:'3.4'
  compile group: 'commons-io', name: 'commons-io', version:'2.4'
  compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.12'
  compile group: 'log4j', name: 'log4j', version:'1.2.16'

  compile group: 'org.apache.poi', name: 'poi', version: '3.17'
  compile group: 'org.apache.poi', name: 'poi-ooxml', version: '3.17'
  compile group: 'org.apache.xmlbeans', name: 'xmlbeans', version: '2.6.0'
  compile group: 'org.apache.poi', name: 'poi-ooxml-schemas', version: '3.17'

  compile group: 'com.fasterxml.jackson.core', name: 'jackson-annotations', version:'2.5.3'
  compile group: 'com.fasterxml.jackson.core', name: 'jackson-core', version:'2.5.3'
  compile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version:'2.5.3'
  compile group: 'org.codehaus.jackson', name: 'jackson-mapper-asl', version:'1.9.13'
  compile group: 'org.codehaus.jackson', name: 'jackson-core-asl', version:'1.9.13'

  compile group: 'org.springframework', name: 'spring-aop', version:springVersion
  compile group: 'org.springframework', name: 'spring-beans',  version:springVersion
  compile group: 'org.springframework', name: 'spring-context', version:springVersion
  compile group: 'org.springframework', name: 'spring-core', version:springVersion
  compile group: 'org.springframework', name: 'spring-jdbc', version:springVersion
  compile group: 'org.springframework.security', name: 'spring-security-core', version:'3.2.7.RELEASE'
  compile group: 'org.springframework', name: 'spring-tx', version:springVersion
  compile group: 'org.springframework', name: 'spring-web', version:springVersion
  compile group: 'org.springframework', name: 'spring-webmvc', version:springVersion
}

compileJava {
  doLast{
    ant.taskdef(resource: "org/aspectj/tools/ant/taskdefs/aspectjTaskdefs.properties",
                classpath: configurations.ajc.asPath)

    ant.iajc(
      source: 1.8, target: 1.8,
      destDir: sourceSets.main.output.classesDir.absolutePath,
      maxmem: "512m", fork: "true",
      aspectPath: configurations.aspects.asPath,
      sourceRootCopyFilter: "**/*.java",
      classpath: "${configurations.compile.asPath};${configurations.aspectCompile.asPath}") {
        sourceroots {
          sourceSets.main.java.srcDirs.each {
            pathelement(location:it.absolutePath)
          }
        }
      }
  }
}

task vue_build(type: Exec) {
  workingDir './src/main/ui'
  commandLine 'npm', 'run', 'build-bundle'
}

fatJar.dependsOn vue_build
build.dependsOn fatJar

